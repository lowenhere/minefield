module mojo_top (
    input clk,              // 50MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    input cclk,             // configuration clock, AVR ready when high
    output spi_miso,        // AVR SPI MISO
    input spi_ss,           // AVR SPI Slave Select
    input spi_mosi,         // AVR SPI MOSI
    input spi_sck,          // AVR SPI Clock
    output spi_channel [4], // AVR general purpose pins (used by default to select ADC channel)
    input avr_tx,           // AVR TX (FPGA RX)
    output avr_rx,          // AVR RX (FPGA TX)
    input avr_rx_busy,      // AVR RX buffer full
    output io_led [3][8],   // LEDs on IO Shield
    output io_seg [8],      // 7-segment LEDs on IO Shield
    output io_sel [4],      // Digit select on IO Shield
    input io_button [5],    // 5 buttons on IO Shield - 0:top 1:center 2:bottom 3:left 4:right
    input io_dip [3][8],     // DIP switches on IO Shield
    
    input controller[8],
    output sound_output,
    output matrix_col[4],
    output matrix_row_sel[2],
    output matrix_rgb_sel[2]
    ) {
  
  sig rst;                  // reset signal
  sig change_stage;
  
  .clk(clk) {
    // The reset conditioner is used to synchronize the reset signal to the FPGA
    // clock. This ensures the entire FPGA comes out of reset at the same time.
    reset_conditioner reset_cond;
    
    .rst(rst){
      dff player_pos[4][4]; 
      dff end_pos[4][4];
      dff mine_pos[4][4];
      fsm state = {STAGE_SELECT, INIT, PLAYING, GAME_WIN, GAME_LOST};
      
      multi_seven_seg seg_display;
      
      display_module display_module;
      move_player_module move_player_module;
      detect_mine_module detect_mine_module;
      sound_module sound_module;
      
    }
    
    input_handler_module input_handler_module;
    score_calculator_module score_calculator_module;
  }
  
  counter stage_select (#SIZE(4), #DIV(0)); 
  game_map_lut game_map_lut;
  
  alu_sel_module alu_sel_module;
  check_game_over_module check_game_over_module;
  
  alu_16_bit alu;
  bin_to_dec decimal_score (#DIGITS(4));
  
  always {
    reset_cond.in = ~rst_n; // input raw inverted reset signal
    rst = reset_cond.out;   // conditioned reset   
    led = 8h00;             // turn LEDs off
    spi_miso = bz;          // not using SPI
    spi_channel = bzzzz;    // not using flags
    avr_rx = bz;            // not using serial port
    io_led = 3x{{8h00}};    // turn LEDs off
    io_seg = 8hff;          // turn segments off
    io_sel = 4hf;           // select no digits
    seg_display.values = {4b0,4b0,4b0,4b0};
    decimal_score.value = 0;
    change_stage = 0;
    
    
    // INPUTS
    input_handler_module.controller_buttons = 8b00000000;
    input_handler_module.controller_buttons = controller; 
    
    // STAGE SELECT
    stage_select.rst = rst;
    stage_select.clk = change_stage;
    
    // GAME_MAP_LUT
    game_map_lut.map_index = 0;
            
    // DISPLAY
    display_module.game_over = 0;
    display_module.win_loss = 0;
    
    display_module.player_pos = player_pos.q;
    display_module.end_pos = end_pos.q;
        
    matrix_col = display_module.matrix_col;
    matrix_row_sel = display_module.matrix_row_sel;
    matrix_rgb_sel = display_module.matrix_rgb_sel; 
    
    // SOUND_MODULE
    sound_module.mine_close = detect_mine_module.mine_near;
    sound_module.mine_far = detect_mine_module.mine_far;
    sound_output = sound_module.sound_output;
        
    // MOVE_PLAYER_MODULE
    move_player_module.player_pos = 4x{{4b0000}};
    move_player_module.move_direction = 0;
    move_player_module.move_player = 0;
    move_player_module.alu_out = 0;
    
    // DETECT_MINE_MODULE
    detect_mine_module.player_pos = 4x{{4b0000}};
    detect_mine_module.mine_pos = 4x{{4b0000}};
    detect_mine_module.scan_direction = 0;
    detect_mine_module.detect_mine = 0;
    detect_mine_module.alu_out = 0;
    
    // SCORE_CALCULATOR_MODULE
    score_calculator_module.rst = rst;
    score_calculator_module.alu_out = 0;
    score_calculator_module.player_step = move_player_module.step;
    score_calculator_module.calculate_score = 0;
    score_calculator_module.start_game = 0;
        
    // CHECK_GAME_OVER_MODULE
    check_game_over_module.player_pos = 4x{{4b0000}};
    check_game_over_module.end_pos = 4x{{4b0000}};
    check_game_over_module.mine_pos = 4x{{4b0000}};
    
    // ALU 
    alu.a = 0;
    alu.b = 0;
    alu.alufn = 0;
    
    // ALU_SEL
    alu_sel_module.move_player_sel = move_player_module.move_player_sel;
    alu_sel_module.detect_mine_sel = detect_mine_module.detect_mine_sel;
    alu_sel_module.calculate_score_sel = score_calculator_module.calculate_score_sel;
    
    // DATAPATH
    case(alu_sel_module.alu_sel){
      2b00:
        alu.a = move_player_module.alu_a;
        alu.b = move_player_module.alu_b;
        alu.alufn = move_player_module.alufn;
        move_player_module.alu_out = alu.o;
      
      2b01:
        alu.a = detect_mine_module.alu_a;
        alu.b = detect_mine_module.alu_b;
        alu.alufn = detect_mine_module.alufn;
        detect_mine_module.alu_out = alu.o;
      
      2b10:
        alu.a = score_calculator_module.alu_a;
        alu.b = score_calculator_module.alu_b;
        alu.alufn = score_calculator_module.alufn;
        score_calculator_module.alu_out = alu.o;
        
      2b11: 
        alu.a = 0;
        alu.b = 0;
        alu.alufn = 0;        
    }
    
    
    case(state.q){
      state.STAGE_SELECT:
        player_pos.d = 4x{{4b0}};
        end_pos.d = 4x{{4b0}};
        mine_pos.d = 4x{{4b0}};
        
        decimal_score.value = c{ 12b0, stage_select.value };
        seg_display.values = decimal_score.digits;
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;  
        io_led[2][3:0] = stage_select.value;      
        
        if (input_handler_module.select ||io_button[2]){
          state.d = state.INIT;
          }
        else if (input_handler_module.move && (input_handler_module.direction == 1)){
          stage_select.clk = 1;
          }
        
      state.INIT:
        game_map_lut.map_index = stage_select.value;
        player_pos.d = game_map_lut.player_pos;
        end_pos.d = game_map_lut.end_pos;
        mine_pos.d = game_map_lut.mine_pos;
        
        state.d = state.PLAYING;

                
      state.PLAYING:
        io_led[1] = score_calculator_module.timer;
        // Connect move_player_module and position dffs
        move_player_module.player_pos = player_pos.q;
        player_pos.d = move_player_module.new_player_pos;
        
        // Connect detect_mine_module and position dffs
        detect_mine_module.player_pos = player_pos.q;
        detect_mine_module.mine_pos = mine_pos.q;
        
        // Connect check_game_over_module and position dffs
        check_game_over_module.player_pos = player_pos.q;
        check_game_over_module.end_pos = end_pos.q;
        check_game_over_module.mine_pos = mine_pos.q;
        
        
        if (input_handler_module.restart_game){
          state.d = state.STAGE_SELECT;
          score_calculator_module.rst = 1;
          }
        else if (input_handler_module.move){
          score_calculator_module.start_game = 1;
          move_player_module.move_player = 1;
          move_player_module.move_direction = input_handler_module.direction;
          }
        else if (input_handler_module.scan){
          score_calculator_module.start_game = 1;
          detect_mine_module.detect_mine = 1;
          detect_mine_module.scan_direction = input_handler_module.direction;
          }
        
        if (check_game_over_module.game_over && check_game_over_module.win_loss){
          state.d = state.GAME_WIN;
          }
        else if (check_game_over_module.game_over && !check_game_over_module.win_loss){
          state.d = state.GAME_LOST;
          }
        

      state.GAME_WIN:
        score_calculator_module.calculate_score = 1;
        
        decimal_score.value = score_calculator_module.score;
        seg_display.values = decimal_score.digits;
        io_seg = ~seg_display.seg;
        io_sel = ~seg_display.sel;
        
        display_module.game_over = 1;
        display_module.win_loss = 1;
        
        if (input_handler_module.restart_game){
          state.d = state.STAGE_SELECT;
          score_calculator_module.rst = 1;
          }
     
      state.GAME_LOST:
        display_module.game_over = 1;
        display_module.win_loss = 0;
        
        if (input_handler_module.restart_game){
          state.d = state.STAGE_SELECT;
          score_calculator_module.rst = 1;
          }
      
    }
     
    
  }
}